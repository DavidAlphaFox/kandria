<article><style>article{
    max-width: 800px;
    font-size: 12pt;
    font-family: sans-serif;
    margin: 3em auto;
}

article h1{
    text-align: center;
    font-size: 2em;
}

article img{
    margin: 0 auto;
    max-width: 100%;
}

article blockquote{
    border-left: 0.2em solid gray;
    margin-left: 1em;
    padding-left: 1em;
}

article figcaption{
    padding: 0.2em 1em;
    background: #E0E0E0;
}

article code{
    background: #F0F0F0;
    padding: 0 0.1em;
}

article .code-block{
    padding: 0.1em 0.5em;
}</style><h1 id="Abstract Definition Language">Abstract Definition Language</h1><p>This document specifies a concise, simple abstract definition language to describe the structure of data, independent of the format used to encode the data. As such, specifications written in ADL are purely concerned with data types and relations.</p><h2 id="Base Syntax">Base Syntax</h2><p>The basic syntax of ADF is described here using the standard Extended Backus-Naur Form.</p><code style="display:block" class="code-block" data-language="BNF"><pre>definition      ::= type-definition+
type-definition ::= type-name ':' (relation | sequence | one-of-type)
relation        ::= '{' field (',' field)* '}'
field           ::= field-name ':' type
sequence        ::= '[' type (',' type) ']'
type            ::= type-name | base-type | compound-type
base-type       ::= 'null' | 'boolean' | 'integer' | 'real' | 'string' | 'symbol' | 'pathname'
compound-type   ::= array-type | table-type | optional-type | one-of-type | any-type
array-type      ::= '[' type ']'
table-type      ::= '{' type ':' type '}'
optional-type   ::= '_' type
one-of-type     ::= '(' type ('|' type)* ')'
any-type        ::= 'T'
type-name       --- A sequence of characters to name a new type.</pre></code><p>White space may be inserted between any of the tokens as desired.</p><p>The <code>type-name</code>s occurring in the definition cannot contain whitespace characters, or one of the following characters: <code>{}[]:,</code>. When a <code>type-name</code> is part of a <code>type</code> rather than part of a <code>type-definition</code>, then there must be a <code>type-definition</code> within the <code>definition</code> with the same <code>type-name</code>.</p><p>A valid ADL definition must be composed solely of a <code>definition</code>.</p><p>The value of an <code>optional-type</code> may either be of the specified <code>type</code>, or <code>null</code>.</p><p>If the <code>type</code> of a <code>field</code> is an <code>optional-type</code>, the <code>field</code> may be omitted entirely, in which case the field's value is always of type <code>null</code>.</p><h2 id="Base Types">Base Types</h2><p>The base-types describe the following kind of types of data:</p><ul><li><p><code>null</code> A non-value.</p></li><li><p><code>boolean</code> A binary &quot;true&quot; or &quot;false&quot; value.</p></li><li><p><code>integer</code> An unbounded integer that may be positive or negative.</p></li><li><p><code>real</code> An unbounded real number.</p></li><li><p><code>string</code> A sequence of Unicode codepoints.</p></li><li><p><code>symbol</code> A name for a named resource.</p></li><li><p><code>pathname</code> A designator for a file on disk as a relative path.</p></li></ul><h2 id="Example">Example</h2><p>An example ADL definition follows:</p><code style="display:block" class="code-block" data-language="ADL"><pre>player: {
  location: vector,
  items: [ symbol ],
  status: { symbol: boolean }
}

vector: {
  x: real,
  y: real
}</pre></code><h2 id="S-Expression Format">S-Expression Format</h2><p>The following section describes the encoding of ADL types in a format known as &quot;s-expression&quot; or &quot;sexp&quot;.</p><code style="display:block" class="code-block" data-language="BNF"><pre>relation      ::= '(' entry+ ')'
entry         ::= field-name value
sequence      ::= value*
null          ::= 'NIL'
boolean       ::= 'NIL' | 'T'
integer       ::= '\d'+
rational      ::= '\d'+ ('.' \d*)?
string        ::= '&quot;' ('\&quot;' | '\'? [^&quot;]*)* '&quot;'
symbol        ::= (package ':')? name
package       ::= name
pathname      ::= '#p' string
array-type    ::= '(' value* ')'
table-type    ::= '(' ('(' value '.' value ')')* ')'
optional-type ::= value | 'NIL'
field-name    --- The name of the field as a keyword symbol.
value         --- A value of the type described in the ADL definition.
name          --- A name composed of characters that are neither whitespace, nor any of the following characters: .:#&quot;()</pre></code></article>